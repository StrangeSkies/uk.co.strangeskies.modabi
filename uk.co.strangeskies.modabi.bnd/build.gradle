/*
 * Modabi Bnd plugin Gradle build script
 */
def modabiPluginProjects() {
  return rootProject.subprojects.findAll {
    it.hasProperty('bnd') && it.bnd.project.getDependson().contains(owner.bnd.project)
  }
}

/* After building, we need to refresh the bnd Projects to pickup the bnd plugins built. */
task('refresh') {
  onlyIf {
    compileJava.didWork || processResources.didWork
  }
  doLast {
    logger.info "Refresh bnd Projects after building bnd plugins in ${project.name}"

    bndWorkspace.clear()
    bndWorkspace.refresh()

    configure(modabiPluginProjects()) {
      logger.info "Refreshing: ${project.bnd.project.name}"

      project.bnd.project.clear()
      project.bnd.project.refresh()
    }
  }
}
build.finalizedBy(refresh)

/* Bnd plugins influence bundle contents, so add generated output to jar inputs. */
def bndProject = project
def generated = project.file("generated")
configure(modabiPluginProjects()) {
  jar {
    logger.info "Add output: ${generated} to ${project}"

    inputs.files generated
    dependsOn ":${bndProject.name}:build"
  }
}
